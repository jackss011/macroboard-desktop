apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'cpp'

sourceCompatibility = 1.8

def appName = project.name
version '1.0-SNAPSHOT'


// ||============================
// ||==>  C++
// ||============================

model {
    platforms {
        x64 {
            architecture 'x64'
        }
    }

    binaries {
        all {
            if (toolChain in VisualCpp) {
                linker.args 'user32.lib'
            }
        }
    }

    components {
        library(NativeLibrarySpec) {
            baseName 'macroboard'   // library file name to import
            targetPlatform 'x64'

            sources {
                cpp {
                    source {
                        srcDirs 'native'
                        include '**/*.cpp'
                    }
                }
            }
        }
    }
}


/** FileCollection of native libraries (dll, do, dylib) which will be included in the packaged app */
def nativeLibs = fileTree("build/libs/library/shared") {
    include '*.dll'
    include '*.so'
    include '*.dylib'
}


/** Move native libraries to other locations */
task moveDll(type: Copy, dependsOn: 'librarySharedLibrary') {
    group 'app'
    from nativeLibs
    into 'build/native'
}
tasks.findByName('build').dependsOn 'moveDll'



// ||============================
// ||==>  JAVA
// ||============================

/** Path to the folder containing the packaged app */
def ARTIFACTS_OUT_DIR = "build/artifacts/$appName"

/** Path representing the package where resources will be moved after .class(s) compiling. */
def RESOURCE_PACKAGE = 'macroboard'

repositories {
    mavenCentral()
}


dependencies {
    compile 'net.java.dev.jna:jna:4.4.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }
        resources {
            srcDirs = ['src/res']
            exclude '**/*.sass'
        }
        output.resourcesDir "${output.classesDir}/$RESOURCE_PACKAGE"
    }
}


/** Package the entire app in a folder */
task('package', type: Copy, dependsOn: ['packJar', 'librarySharedLibrary']) {
    group 'app'

    from nativeLibs
    into "$ARTIFACTS_OUT_DIR/bin"
}


/** Package java code in a Jar file */
task packJar(type: Jar, dependsOn: 'classes') {
    manifest { attributes("Main-Class": "macroboard.Main") }
    from sourceSets.main.output.classesDir    //main source set
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }  //include libraries
    destinationDir file(ARTIFACTS_OUT_DIR)
}



// ||============================
// ||==>  SASS
// ||============================

def final MAIN_SASS_DIR = 'src/res/main.sass'
def final BUILT_CSS_DIR = "${sourceSets.main.output.resourcesDir}/main.css"
def final SASS_OPT = '--sourcemap=none'

/** Compile sass and output a css to resources dir */
task compileSass(type: Exec) {
    group 'app'
    mustRunAfter 'classes'

    doFirst {
        file(BUILT_CSS_DIR).getParentFile().mkdirs()
    }

    if(System.getProperty("os.name").toLowerCase().contains("windows"))
    {
        commandLine 'cmd', '/c', 'sass', SASS_OPT, MAIN_SASS_DIR, BUILT_CSS_DIR
    }
    else
    {
        executable 'sass'
        args SASS_OPT, MAIN_SASS_DIR, BUILT_CSS_DIR
    }
}
tasks.getByName('build').dependsOn 'compileSass'
tasks.getByName('package').dependsOn 'compileSass'